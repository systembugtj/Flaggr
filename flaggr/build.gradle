buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def repo_context_url = hasProperty('artifactory_context_url') ? artifactory_context_url : System.getenv('artifactory_context_url')
def repo_username = hasProperty('artifactory_username') ? artifactory_username : System.getenv('artifactory_username')
def repo_password = hasProperty('artifactory_password') ? artifactory_password : System.getenv('artifactory_password')
def repo_key = hasProperty('artifactory_key') ? artifactory_key : System.getenv('artifactory_key')
def VERSION_NAME = System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "1.0.0"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {

        minSdkVersion 16
        targetSdkVersion 25
        versionCode 2
        versionName "0.3.1 "

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    tasks.withType(Javadoc).all { enabled = false }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
    jcenter()
}

dependencies {
    provided 'com.squareup.okhttp3:okhttp:3.4.1'
    provided 'com.squareup.okio:okio:1.11.0'

    compile 'com.google.code.gson:gson:2.5'
    compile 'com.android.support:support-annotations:23.4.0'
    compile 'com.google.dagger:dagger:2.4'
    compile 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.android.support.test:runner:0.5'
    apt 'com.google.dagger:dagger-compiler:2.4'

    testCompile 'junit:junit:4.12'
    androidTestCompile "junit:junit:4.12"
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'
}

def libraryGroupId = 'com.conouch.feature'
def libraryArtifactId = 'flaggr'
def libraryVersion = VERSION_NAME

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId

            artifact("$buildDir/outputs/aar/${project.name}-release.aar")
        }
    }
}

artifactory {
    contextUrl = repo_context_url
    publish {
        repository {
            repoKey = repo_key

            username = repo_username
            password = repo_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}

